load IrcClient

host = nil
channel = nil
port = 6667

cmd = cmdP()
cmd.parse()

if (host == nil) or (channel == nil)
    > "You must give a host and channel"
    exit(1)
end

test = IrcClient(host, port, "TechnoBot", "Techno's Bot", "TechnoBot")

test.connect()
test.send(@"JOIN $channel")

subscribe("IrcPrivateMessage", privMessageHandler)
subscribe("IrcNoticeMessage", messageHandler)
subscribe("IrcMessage", messageHandler)



function messageHandler(message)
    > message
end

function privMessageHandler(message)
    > message
    if message.channel == channel
        said = message.said.lower()
        if said.find("technobot") >= 0
            if said.find("quit") >= 0
                message.client.quit()
                exit(0)
            else
                message.client.sendTo(channel, "Is someone talking about me?")
            end
        end
    end
end

class cmdP from CmdlineParser
    
    function onOption( option )
        switch option
            case "h", "host", "p", "port", "c", "channel"
                self.expectValue()
            default
                > "Unrecognised option"
                exit(1)
        end
    end
   
    function onValue( option, value)
        global host, port, channel
        switch option
            case "h", "host"
                > "host: " + value
                host = value
            case "p", "port"
                > "port: " + value
                port = value
            case "c", "channel"
                > "channel: " + value
                channel = value
        end
    end
   
end